#------------------------------------------------------------------------------
# Variables:
#------------------------------------------------------------------------------

pkgname=dwm
pkgver=6.1
pkgrel=3
pkgdesc='A dynamic window manager for X'
url='http://dwm.suckless.org'
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
install=dwm.install

source=(http://dl.suckless.org/dwm/dwm-${pkgver}.tar.gz
        config.origin
	      config.h
	      dwm.desktop)

patches=(01-statuscolors.diff
         02-noborder.diff
         03-pertag.diff
         04-htile.diff)

source=(${source[@]} ${patches[@]})

#------------------------------------------------------------------------------
# MD5 Sums: (makepkg -g)
#------------------------------------------------------------------------------

md5sums=('f0b6b1093b7207f89c2a90b848c008ec'
         '80c4ef2a3eca0fe2d14e2203e3833200'
         'a5d4af01fae0eeb801a3152f26513f1e'
         '939f403a71b6e85261d09fc3412269ee'
         'ed406bed8c3278d99d3ff556f5f4f228'
         'cd07a1ee08ebca41ecbbc0e4dd81d5f6'
         '3126263695676ffa7ab7d90e49bd77cb'
         '9207bfeb3d417799512dbe88c388a2ff')

#------------------------------------------------------------------------------
# Prepare:
#------------------------------------------------------------------------------

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp ${srcdir}/config.h config.h
}

#------------------------------------------------------------------------------
# Build:
#------------------------------------------------------------------------------

build() {

  cd ${srcdir}/${pkgname}-${pkgver}
  cp ${srcdir}/config.origin config.h

  for p in "${patches[@]}"; do
    echo "=> ${p}"
    patch < ../${p} || return 1
  done

  cp ${srcdir}/config.h config.h
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

#------------------------------------------------------------------------------
# Package:
#------------------------------------------------------------------------------

package() {

  cd ${srcdir}/${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR=${pkgdir} install
  install -m644 -D LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -m644 -D README ${pkgdir}/usr/share/doc/${pkgname}/README
  install -m644 -D ${srcdir}/dwm.desktop ${pkgdir}/usr/share/xsessions/dwm.desktop
}
